description = "Automated tool for deploying shathel packages to docker machine swarm cluster"
ext {
    displayName = "Shathel Deployer"
}

apply from: '../gradle/spring.gradle'
apply from: '../gradle/bintray.gradle'

apply plugin: 'spring-boot'
apply plugin: 'scala'

dependencies {

    compile 'org.scala-lang:scala-library:2.12.1'
    compile project(":shathel-commons")
    compile 'com.squareup.okhttp3:okhttp:3.6.0'
    compile 'org.beryx:text-io:1.7.0'
    compile 'jline:jline:2.14.3'
    compile('org.springframework.shell:spring-shell:1.2.0.M1') {
        //https://github.com/jline/jline2/issues/205
        exclude group: 'jline'
        exclude group: 'com.google.guava'
    }
    compile('org.springframework.boot:spring-boot-starter')

    testCompile "org.scalactic:scalactic_2.12:3.0.1"
    testCompile "org.scalatest:scalatest_2.12:3.0.1"
}


springBoot {
    executable = true
    embeddedLaunchScript = file('src/main/resources/launch.script.sh')
}

apply from: '../gradle/github.gradle'

task(type: GitHubReleaseUploadAsset, dependsOn: assemble, "uploadGithubRelease") {
    markAsNonPre = true
    asset(file("$project.buildDir/libs/$project.name-${project.version}.jar"),
            [name : "$project.name",
             label: "Runnable $project.name jar"]
    )
}

task(type: Copy, dependsOn: assemble, "dockerPrepare") {
    from("src/main/docker") {
    }
    from("$project.buildDir/libs/") {
        include "$project.name-${project.version}.jar"
        rename { x ->
            "shathel-deployer"
        }
    }
    into "$project.buildDir/docker"
}
task(type: Exec, dependsOn: dockerPrepare, "dockerBuild") {
    workingDir "$project.buildDir/docker"
    commandLine "docker","build", "-t", "sasol/shathel:${project.version}", "."
    inputs.dir("$project.buildDir/docker")
    outputs.file("$project.buildDir/docker.done")
    doLast {
        file("$project.buildDir/docker.done").text = java.util.UUID.randomUUID().toString()
    }
}
task(type: Exec, dependsOn: dockerBuild, "dockerPush") {
    workingDir "$project.buildDir/docker"
    commandLine "docker","push", "sasol/shathel:${project.version}"
}


apply from: '../gradle/integration.gradle'

install.dependsOn assemble
test.dependsOn install

