description = "Automated tool for deploying shathel packages to docker machine swarm cluster"
ext {
    displayName = "Shathel Deployer"
    aetherVersion = "1.1.0"
    mavenVersion = "3.1.0"
    wagonVersion = "1.0"
}

apply from: '../gradle/spring.gradle'
apply from: '../gradle/bintray.gradle'

apply plugin: 'spring-boot'
apply plugin: 'scala'

dependencies {

    compile 'org.scala-lang:scala-library:2.12.1'
    compile 'org.apache.ant:ant:1.8.2'
    compile "org.eclipse.aether:aether-api:${aetherVersion}"
    compile("org.eclipse.aether:aether-ant-tasks:1.0.1.v20141111") {
        exclude group: 'org.slf4j', module: 'slf4j-nop'
    }
    compile "org.eclipse.aether:aether-util:${aetherVersion}"
    compile "org.eclipse.aether:aether-impl:${aetherVersion}"
    compile "org.eclipse.aether:aether-connector-basic:${aetherVersion}"
    compile "org.eclipse.aether:aether-transport-file:${aetherVersion}"
    compile "org.eclipse.aether:aether-transport-http:${aetherVersion}"
    compile "org.eclipse.aether:aether-transport-wagon:${aetherVersion}"
    compile "org.apache.maven:maven-aether-provider:${mavenVersion}"
    compile "org.apache.maven.wagon:wagon-ssh:${wagonVersion}"
    compile project(":shathel-commons")
    compile 'org.beryx:text-io:1.7.0'
    compile 'jline:jline:2.14.3'
    compile('org.springframework.shell:spring-shell:1.2.0.M1') {
        //https://github.com/jline/jline2/issues/205
        exclude group: 'jline'
        exclude group: 'com.google.guava'
    }
    compile('org.springframework.boot:spring-boot-starter')

    testCompile "org.scalactic:scalactic_2.12:3.0.1"
    testCompile "org.scalatest:scalatest_2.12:3.0.1"
}


springBoot {
    executable = true
}

apply from: '../gradle/github.gradle'

task(type: GitHubReleaseUploadAsset, dependsOn: assemble, "uploadGithubRelease") {
    markAsNonPre = true
    asset(file("$project.buildDir/libs/$project.name-${project.version}.jar"),
            [name : "$project.name",
             label: "Runnable $project.name jar"]
    )
}

apply from: '../gradle/integration.gradle'

install.dependsOn assemble
integrationTest.dependsOn install
project.rootProject.subprojects.findAll {it.name.startsWith("shathel-stack-")}.each {
    evaluationDependsOn("$it.path")
    integrationTest.dependsOn it.install
}
